# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>
# Maintainer: DarkMatterCore <pabloacurielz@gmail.com>

pkgbasename=ntfs-3g_ntfsprogs
pkgname=switch-libntfs-3g
pkgver=2021.8.22
pkgrel=1
pkgdesc='Read-Write NTFS Driver'
baseurl='https://www.tuxera.com'
url="${baseurl}/community/open-source-ntfs-3g/"
license=('GPL')
arch=('any')
options=(!strip libtool staticlibs)
makedepends=('devkitA64' 'dkp-toolchain-vars')
groups=('switch-portlibs')
source=(
  "${baseurl}/opensource/${pkgbasename}-${pkgver}.tgz"
  "${pkgname}-${pkgver}.patch"
)
sha256sums=(
  '55b883aa05d94b2ec746ef3966cb41e66bed6db99f22ddd41d1b8b94bb202efb'
  'f66e80896f2dba933f790804e1e21fe11e7347a6e9bd0dc1323c718d826378f4'
)

prepare() {
  cd "${pkgbasename}-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}.patch"

  source "${DEVKITPRO}/switchvars.sh"
}

build() {
  cd "${pkgbasename}-${pkgver}"

  aclocal
  automake

  ./configure --prefix="${PORTLIBS_PREFIX}" --libdir="${PORTLIBS_PREFIX}/lib" --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-device-default-io-ops \
    --disable-ntfs-3g --disable-ntfsprogs --disable-plugins --disable-crypto \
    --without-uuid --without-hd

  make
}

package() {
  cd "${pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE"
  install -Dm644 config.h "${pkgdir}${PORTLIBS_PREFIX}/include/ntfs-3g/config.h"
}

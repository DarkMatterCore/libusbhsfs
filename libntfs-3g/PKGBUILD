# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>
# Maintainer: DarkMatterCore <pabloacurielz@gmail.com>

pkgbasename=ntfs-3g_ntfsprogs
pkgname=switch-libntfs-3g
pkgver=2022.5.17
pkgrel=1
pkgdesc='Read-Write NTFS Driver'
baseurl='https://www.tuxera.com'
url="${baseurl}/community/open-source-ntfs-3g/"
license=('GPL')
arch=('any')
options=(!strip libtool staticlibs)
makedepends=('devkitA64' 'dkp-toolchain-vars')
groups=('switch-portlibs')
source=(
  "${baseurl}/opensource/${pkgbasename}-${pkgver}.tgz"
  "${pkgname}-${pkgver}.patch"
)
sha256sums=(
  '0489fbb6972581e1b417ab578d543f6ae522e7fa648c3c9b49c789510fd5eb93'
  '2fdad1baea042fb30e3e8eb5732c0d9a80a88a77c53bad33e215f42a3606826c'
)

prepare() {
  cd "${pkgbasename}-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}.patch"

  source "${DEVKITPRO}/switchvars.sh"
}

build() {
  cd "${pkgbasename}-${pkgver}"

  aclocal
  automake

  ./configure --prefix="${PORTLIBS_PREFIX}" --libdir="${PORTLIBS_PREFIX}/lib" --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-device-default-io-ops \
    --disable-ntfs-3g --disable-ntfsprogs --disable-plugins --disable-crypto \
    --without-uuid --without-hd

  make
}

package() {
  cd "${pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE"
  install -Dm644 config.h "${pkgdir}${PORTLIBS_PREFIX}/include/ntfs-3g/config.h"
}

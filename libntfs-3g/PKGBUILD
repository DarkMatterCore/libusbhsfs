# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>
# Maintainer: DarkMatterCore <pabloacurielz@gmail.com>

pkgname=switch-libntfs-3g
pkgver=2017.3.23
pkgrel=1
pkgdesc='Read-Write NTFS Driver'
baseurl='https://www.tuxera.com'
url="${baseurl}/community/open-source-ntfs-3g/"
license=('GPL')
arch=('any')
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('switch-portlibs')
source=("${baseurl}/opensource/ntfs-3g_ntfsprogs-${pkgver}.tgz")
sha256sums=('3e5a021d7b761261836dcb305370af299793eedbded731df3d6943802e1262d5')

prepare() {
  cd "ntfs-3g_ntfsprogs-${pkgver}"

  patch -Np1 -i "${srcdir}/../ntfs-3g_ntfsprogs-${pkgver}.patch"
}

build() {
  cd "ntfs-3g_ntfsprogs-${pkgver}"

  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-device-default-io-ops \
    --disable-ntfs-3g --disable-ntfsprogs --disable-plugins --disable-crypto \
    --without-uuid --without-hd

  make
}

package() {
  cd "ntfs-3g_ntfsprogs-${pkgver}"

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE"
  install -Dm644 config.h "${pkgdir}${PORTLIBS_PREFIX}/include/ntfs-3g/config.h"
}
